# Maintainer: MedzikUser
#
_repo='cosmtrek/air'
_ver=v1.27.10
_auto_update=true

_pkgname='air'
pkgname="${_pkgname}"
pkgver=1.27.10
pkgrel=1
pkgdesc='Live reload for Go apps'
arch=('x86_64')
url="https://github.com/${_repo}"
license=('GPL3')

depends=('glibc')
makedepends=('git' 'go')
optdepends=(
  'go: for development go apps'
  'nodejs: for development node.js apps'
)

source=("${_pkgname}::git+${url}#tag=${_ver}")
sha256sums=('SKIP')

build() {
  cd "${_pkgname}"

  local _flags
  _flags=(
    -s -w
    -X main.Version="${pkgver}"
    -X main.BuildTime="$(date +%Y.%m.%d-%H:%M:%S)"
  )

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -modcacherw"

  go build \
    -v \
    -ldflags "${_flags[*]}" \
    -o bin/${_pkgname} \
    .
}

package() {
  cd "${_pkgname}"

  install -Dm 755 bin/${_pkgname} "${pkgdir}/usr/bin/${_pkgname}"
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
