# Maintainer: MedzikUser <nivua1fn@duck.com>
pkgname='assemblyscript'
conflicts=('typescript')
pkgver='0.20.7'
pkgrel=1
pkgdesc='TypeScript to WebAssembly compiler'
arch=('any')
url="https://www.npmjs.com/package/$pkgname"
license=('Apache')
depends=('nodejs')
makedepends=('npm')
_file="$pkgname-$pkgver.tgz"
source=("https://registry.npmjs.org/$pkgname/-/$_file")
noextract=("$_file")
sha256sums=('c5629cf22f4a5fcd735ef7338827f7786aeaf58118d6098e8bbbb077595bcc2f')

package() {
  npm install -g --prefix "$pkgdir/usr" "$_file"

  # Delete .map files
  find "$pkgdir"/usr/lib -depth -name '*.map' -delete

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
