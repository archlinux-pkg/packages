# Maintainer: MedzikUser <nivua1fn@duck.com>\
_name='Beekeeper Studio'
_pkgname='beekeeper-studio'

pkgname="$_pkgname-bin"
provides=("$_pkgname")
conflicts=("$_pkgname")
pkgver='3.3.1'
pkgrel=1
pkgdesc='Modern and easy to use SQL client for MySQL, Postgres, SQLite, SQL Server, and more'
arch=('x86_64')
url='https://www.beekeeperstudio.io'
_url_source='https://github.com/beekeeper-studio/beekeeper-studio'
license=('GPL3')

depends=('libappindicator-gtk3' 'libnotify' 'libsecret' 'libxss' 'libxslt' 'nodejs' 'nss' 'xdg-utils')

source=("LICENSE.md::${_url_source}/raw/v${pkgver}/LICENSE.md"
        "$_url_source/releases/download/v$pkgver/${_pkgname}_${pkgver}_amd64.deb")
sha256sums=('1409fbbc5265c85da91684660c87f85d74c3fdc63a2d355169f40dac5cc7a078'
            'fef66d29de018bed482563b131af2961eb78131cb7bb7b37d5fa6850919f059c')

package() {
  tar -xvf 'data.tar.xz' -C "$pkgdir"

  rm -rf "$pkgdir/usr/share/doc"
  install -dv "$pkgdir/usr/bin"
  ln -sfv "/opt/$_name/$_pkgname" -t "$pkgdir/usr/bin"
  install -Dvm644 "$pkgdir/opt/$_name/"{'LICENSE.electron.txt','LICENSES.chromium.html'} \
    -t "$pkgdir/usr/share/licenses/$_pkgname"
  install -Dvm644 "LICENSE.md" "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"
}
