# Maintainer: MedzikUser <nivua1fn@duck.com>
pkgname='gatsby-cli'
replaces=("nodejs-$pkgname")
provides=("nodejs-$pkgname")
pkgver='4.12.0'
pkgrel=1
pkgdesc='Gatsby command-line interface for creating new sites and running Gatsby commands'
arch=('any')
url="https://www.npmjs.com/package/$pkgname"
license=('MIT')

depends=('nodejs')
makedepends=('npm')

_file="$pkgname-$pkgver.tgz"
source=("https://registry.npmjs.org/$pkgname/-/$_file")
noextract=("$_file")
sha512sums=('9f9ff2ec8053353eaaed31b2bf6f3a9c4b60698414a07da4b6ffbb9d28561e88d7384fcfe61db0469b670e84f3ee0e640916d5a12b9e5c4e61a4b58cf525c553')

package() {
  npm install -g --prefix "$pkgdir/usr" "$_file"

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
