# Maintainer: MedzikUser <nivua1fn@duck.com>
pkgname='gatsby-cli'
replaces=("nodejs-$pkgname")
provides=("nodejs-$pkgname")
pkgver='4.11.1'
pkgrel=2
pkgdesc='Gatsby command-line interface for creating new sites and running Gatsby commands'
arch=('any')
url="https://www.npmjs.com/package/$pkgname"
license=('MIT')

depends=('nodejs')
makedepends=('npm')

_file="$pkgname-$pkgver.tgz"
source=("https://registry.npmjs.org/$pkgname/-/$_file")
noextract=("$_file")
sha512sums=('443385233280cb2b1ae75c7498ca0cb5d561c8e5f652406e1327a51aaa6e72197c6ff75d91afdc8c46079374114aae79f9c04dd3fd5c4c7b7f035dfda2800a52')

package() {
  npm install -g --prefix "$pkgdir/usr" "$_file"

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
