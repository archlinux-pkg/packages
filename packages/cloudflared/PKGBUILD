# Maintainer: MedzikUser

_repo='cloudflare/cloudflared'
_ver=2022.1.3
_auto_update=true

_pkgname='cloudflared'
pkgname="${_pkgname}"
pkgver=2022.1.3
pkgrel=1
pkgdesc='Cloudflare Tunnel client (formerly Argo Tunnel)'
arch=('x86_64')
url="https://github.com/${_repo}"
license=('custom')

depends=('glibc')
makedepends=('git' 'go')

source=("${_pkgname}::git+${url}#tag=${_ver}")
sha256sums=('SKIP')

build() {
  cd "${_pkgname}"

  local _flags
  _flags=(
    -s -w
    -X main.Version="$pkgver"
    -X main.BuildTime="$(date +%Y.%m.%d-%H:%M:%S)"
  )

  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -modcacherw"

  go build \
    -v \
    -ldflags "${_flags[*]}" \
    -o bin/${_pkgname} \
    ./cmd/${_pkgname}
}

package() {
  cd "${_pkgname}"

  install -Dm 755 bin/${_pkgname} "${pkgdir}/usr/bin/${_pkgname}"
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
