# Maintainer: MedzikUser <nivua1fn@duck.com>
_repo='cloudflare/cloudflared'
_ver=2022.3.2
_auto_update=true

pkgname='cloudflared'
pkgver=2022.3.2
pkgrel=2
pkgdesc='Cloudflare Tunnel client (formerly Argo Tunnel)'
arch=('x86_64')
url="https://github.com/$_repo"
license=('Apache')

makedepends=('go')

source=("$pkgname-$pkgver.tar.gz::$url/archive/$_ver.tar.gz")
sha256sums=('58ed53896b7ee2cc4ac84ec2d6f47423476b4292a56b5379aa9715103a4cb226')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  local _flags
  _flags=(
    -X main.Version="$pkgver"
    -X main.BuildTime=$(date -u '+%Y.%m.%d-%H:%M UTC')
  )

  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -modcacherw"

  go build \
    -v \
    -ldflags "${_flags[*]}" \
    -o bin/$pkgname \
    ./cmd/$pkgname
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -Dm 755 bin/$pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
