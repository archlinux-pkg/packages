# Maintainer: MedzikUser <nivua1fn@duck.com>
pkgname='cloudflared'
pkgver='2022.3.4'
pkgrel=1
pkgdesc='Cloudflare Tunnel client (formerly Argo Tunnel)'
arch=('x86_64')
url="https://github.com/cloudflare/cloudflared"
license=('Apache')

makedepends=('go')

source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha256sums=('e810ccc855cc25e8a2f33b42d33570edf97856b227baf4ca0def8e47a4b8d387')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  local _flags
  _flags=(
    -X main.Version="$pkgver"
    -X main.BuildTime="$(date -u -d "@$SOURCE_DATE_EPOCH" +'%FT%TZ')"
  )

  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -modcacherw"

  go build \
    -v \
    -ldflags "${_flags[*]}" \
    -o bin/$pkgname \
    ./cmd/$pkgname
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -Dm 755 bin/$pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
