# Maintainer: MedzikUser <nivua1fn@duck.com>
_npm='@ionic/cli'
_ver=6.18.2
_auto_update=true

pkgname='ionic-cli'
pkgver=6.18.2
pkgrel=1
pkgdesc='A tool for creating and developing Ionic Framework mobile apps'
arch=('any')
url="https://www.npmjs.com/package/$_npm"
license=('MIT')

depends=('nodejs')
makedepends=('npm')

_file="cli-$pkgver.tgz"
source=("https://registry.npmjs.org/$_npm/-/$_file")
noextract=("$_file")
sha512sums=('4e6408277c6e2b0af3cb090aa7297a9fa5fcf0e96a09283689327a9ddfe7c68a62d99eb6041702349c390b3ce766a75adfa471e65735c33d8b5958b9ba9da63c')

package() {
  npm install -g --prefix "$pkgdir/usr" "$_file"

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
