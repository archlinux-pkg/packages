# Maintainer: MedzikUser <nivua1fn@duck.com>
_pkgname='@ionic/cli'
pkgname='ionic-cli'
replaces=("nodejs-$pkgname")
provides=("nodejs-$pkgname")
pkgver='6.19.1'
pkgrel=1
pkgdesc='A tool for creating and developing Ionic Framework mobile apps'
arch=('any')
url="https://www.npmjs.com/package/$_pkgname"
license=('MIT')

depends=('nodejs')
makedepends=('npm')

_file="cli-$pkgver.tgz"
source=("https://registry.npmjs.org/$_pkgname/-/$_file")
noextract=("$_file")
sha512sums=('e7009e024572bf3898955cb5bf01cb6c005cb2a7daa30c0b16002438ab65e165d87163af155f95535668c6cdf75c7203c6688192a55c0bf26138b12b966662f6')

package() {
  npm install -g --prefix "$pkgdir/usr" "$_file"

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
