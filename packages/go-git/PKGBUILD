# Maintainer: MedzikUser <nivua1fn@duck.com>
_repo='golang/go'
_ver=baf61e4a67789e20f019507287a324cca06bed42
_auto_update=true
_auto_update_git=true

pkgname=go-git
pkgver=9956a5423e40bab92c572489eae26ba80ed803ab
pkgrel=1
replaces=('go')
provides=('go')
pkgdesc='Core compiler tools for the Go programming language (master branch from git)'
arch=('x86_64')
url='https://golang.org/'
license=('BSD')

makedepends=('git' 'go' 'perl')

source=("$pkgname::git+https://go.googlesource.com/go#branch=master")
sha256sums=('SKIP')

options=(!strip staticlibs)

pkgver() {
  cd "$pkgname"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  export GOARCH=amd64
  export GOAMD64=v1 # make sure we're building for the right x86-64 version
  export GOROOT_FINAL=/usr/lib/go
  export GOROOT_BOOTSTRAP=/usr/lib/go

  cd "$pkgname/src"
  ./make.bash -v
}

check() {
  export GO_TEST_TIMEOUT_SCALE=3

  cd $pkgname/src
  ./run.bash --no-rebuild -v -v -v -k
}

package() {
  cd "$pkgname"

  install -d "$pkgdir/usr/bin" "$pkgdir/usr/lib/go" "$pkgdir/usr/share/doc/go" \
    "$pkgdir/usr/lib/go/pkg/linux_amd64_"{dynlink,race}

  cp -a bin pkg src lib misc api test "$pkgdir/usr/lib/go"
  cp -r doc/* "$pkgdir/usr/share/doc/go"

  ln -sf /usr/lib/go/bin/go "$pkgdir/usr/bin/go"
  ln -sf /usr/lib/go/bin/gofmt "$pkgdir/usr/bin/gofmt"
  ln -sf /usr/share/doc/go "$pkgdir/usr/lib/go/doc"

  rm -rf "$pkgdir/usr/lib/go/pkg/bootstrap" "$pkgdir/usr/lib/go/pkg/tool/*/api"

  # TODO: Figure out if really needed
  rm -rf "$pkgdir"/usr/lib/go/pkg/obj/go-build/*

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
