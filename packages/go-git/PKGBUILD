# Maintainer: MedzikUser <nivua1fn@duck.com>\
pkgname='go-git'
pkgver=git
pkgrel=1
arch=('x86_64')
pkgdesc='Core compiler tools for the Go programming language'
url='https://golang.org/'
license=('BSD')

provides=('go')
conflicts=('go')
makedepends=('git' 'go' 'perl')

source=("git+https://go.googlesource.com/go#branch=master")
sha256sums=('SKIP')

options=(!strip staticlibs)

pkgver() {
  cd "$srcdir/go"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  export GOARCH=amd64
  export GOAMD64=v1 # make sure we're building for the right x86-64 version
  export GOROOT_FINAL=/usr/lib/go
  export GOROOT_BOOTSTRAP=/usr/lib/go

  cd "$srcdir/go/src"
  ./make.bash -v
}

check() {
  export GO_TEST_TIMEOUT_SCALE=3

  cd "$srcdir/go/src"
  ./run.bash --no-rebuild -v -v -v -k
}

package() {
  cd "$srcdir/go"

  install -d "$pkgdir/usr/bin" "$pkgdir/usr/lib/go" "$pkgdir/usr/share/doc/go" \
    "$pkgdir/usr/lib/go/pkg/linux_amd64_"{dynlink,race}

  cp -a bin pkg src lib misc api test "$pkgdir/usr/lib/go"
  cp -r doc/* "$pkgdir/usr/share/doc/go"

  ln -sf /usr/lib/go/bin/go "$pkgdir/usr/bin/go"
  ln -sf /usr/lib/go/bin/gofmt "$pkgdir/usr/bin/gofmt"
  ln -sf /usr/share/doc/go "$pkgdir/usr/lib/go/doc"

  install -Dm644 VERSION "$pkgdir/usr/lib/go/VERSION"

  rm -rf "$pkgdir/usr/lib/go/pkg/bootstrap" "$pkgdir/usr/lib/go/pkg/tool/*/api"

  # TODO: Figure out if really needed
  rm -rf "$pkgdir"/usr/lib/go/pkg/obj/go-build/*

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/go/LICENSE"
}
