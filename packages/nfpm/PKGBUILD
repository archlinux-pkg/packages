# Maintainer: MedzikUser <nivua1fn@duck.com>
pkgname='nfpm'
pkgver='2.15.1'
pkgrel=1
pkgdesc='A simple deb and rpm packager written in Go'
arch=('x86_64')
url="https://github.com/goreleaser/goreleaser"
license=('MIT')

makedepends=('go')

source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('02e5146f180389e03045ec0ad65670971358915d3fd4bc52d7b053d82676ac85a7cbbd683916485b83f0d53aada18d107fbf6c2b73f12302958ce28f8c8d9332')

build() {
  cd "$pkgname-$pkgver"

  local _flags
  _flags=(
    -X main.version="$pkgver"
  )

  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -modcacherw"

  go build \
    -v \
    -ldflags "${_flags[*]}" \
    -o bin/$pkgname \
    ./cmd/nfpm
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 bin/$pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
