# Maintainer: MedzikUser <nivua1fn@duck.com>\
pkgname='aws-cdk'
pkgver='2.17.0'
pkgrel=1
pkgdesc='CDK Toolkit, the command line tool for CDK app'
arch=('any')
url='https://github.com/awslabs/aws-cdk'
license=('Apache')

depends=('nodejs')
makedepends=('npm')

_file="$pkgname-$pkgver.tgz"
source=("$_file::https://registry.npmjs.org/$pkgname/-/$_file")
noextract=("$_file")
sha512sums=('8113cfa5338d3a3b50e3403cb1cd92cd73c60f316555b48f3edb35a633b1e15049d92f7503438ddc591bcbe5d7fd7a9da351884d358093955ae0f9e863252198')

package() {
  npm install -g --prefix "$pkgdir/usr" "$srcdir/$_file"

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
