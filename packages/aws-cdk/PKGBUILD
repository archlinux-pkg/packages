# Maintainer: MedzikUser

_npm='aws-cdk'
_ver=2.9.0
_auto_update=true

_pkgname="${_npm}"
pkgname="${_pkgname}"
pkgver=2.9.0
pkgrel=1
pkgdesc='CDK Toolkit, the command line tool for CDK apps'
arch=('any')
url="https://www.npmjs.com/package/${_npm}"
license=('Apache')

depends=('nodejs')
makedepends=('npm')
optdepends=('python: python language support')

_file="${_npm}-${pkgver}.tgz"
source=("https://registry.npmjs.org/${_npm}/-/${_file}")
noextract=("${_file}")
sha256sums=('SKIP')

package() {
  npm install -g --prefix "${pkgdir}/usr" "${srcdir}/${_file}"

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "${pkgdir}"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "${pkgdir}"
}
