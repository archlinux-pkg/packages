# Maintainer: MedzikUser <nivua1fn@duck.com>
pkgname='aws-cdk'
replaces=("nodejs-$pkgname")
provides=("nodejs-$pkgname")
pkgver='2.24.0'
pkgrel=1
pkgdesc='CDK Toolkit, the command line tool for CDK app'
arch=('any')
url='https://github.com/awslabs/aws-cdk'
license=('Apache')

depends=('nodejs')
makedepends=('npm')

_file="$pkgname-$pkgver.tgz"
source=("$_file::https://registry.npmjs.org/$pkgname/-/$_file")
noextract=("$_file")
sha512sums=('5ea0320f658868a9e09543e578fb7a73b58c2027a6a3a465f513e72aa23420a9e7cd3391b971f2570f62e85f52d23355df5883c36bb164f0d47dc67cde8c2afd')

package() {
  npm install -g --prefix "$pkgdir/usr" "$srcdir/$_file"

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
