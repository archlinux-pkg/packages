# Maintainer: MedzikUser <nivua1fn@duck.com>
_npm='aws-cdk'
_ver=2.16.0
_auto_update=true
_auto_update_npm=true

pkgname="$_npm"
pkgver=2.16.0
pkgrel=1
pkgdesc='CDK Toolkit, the command line tool for CDK app'
arch=('any')
url="https://github.com/awslabs/aws-cdk"
license=('Apache')

depends=('nodejs')
makedepends=('npm')

_file="$_npm-$pkgver.tgz"
source=("$_file::https://registry.npmjs.org/$_npm/-/$_file")
noextract=("$_file")
sha256sums=('084e3406ff86dd84867b33fec9dcb825262f67289c01971ad7fa7fd0e3bb109d')

package() {
  npm install -g --prefix "$pkgdir/usr" "$srcdir/$_file"

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
