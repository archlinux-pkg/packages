# Maintainer: MedzikUser <nivua1fn@duck.com>
pkgname='esbuild'
pkgver='0.14.40'
pkgrel=1
pkgdesc='An extremely fast JavaScript and CSS bundler and minifier'
arch=('x86_64')
url="https://github.com/evanw/esbuild"
license=('MIT')

makedepends=('go')

source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('3f7929ca65ad951a05affc95cdf73f3dd5b7029d5d3b31e6f74b06d27dbfb628')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -modcacherw"

  go build \
    -v \
    -o bin/$pkgname \
    ./cmd/$pkgname
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -Dm 755 bin/$pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
