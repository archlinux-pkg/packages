# Maintainer: MedzikUser

_repo=evanw/esbuild
_ver=v0.14.20
_auto_update=true

_pkgname="esbuild"
pkgname="${_pkgname}"
pkgver=0.14.20
pkgrel=1
pkgdesc='An extremely fast JavaScript and CSS bundler and minifier'
arch=('x86_64')
url="https://github.com/${_repo}"
license=('MIT')

depends=('glibc')
makedepends=('git' 'go')

source=("${_pkgname}::git+${url}.git#tag=${_ver}")
sha256sums=('SKIP')

build() {
  cd "${_pkgname}"

  local _flags
  _flags=(
    -s -w
  )

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -modcacherw"

  go build \
    -v \
    -ldflags "${_flags[*]}" \
    -o bin/${_pkgname} \
    ./cmd/${_pkgname}
}

package() {
  cd "${_pkgname}"

  install -Dm 755 bin/${_pkgname} "${pkgdir}/usr/bin/${_pkgname}"
  install -Dm 644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
