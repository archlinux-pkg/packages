# Maintainer: MedzikUser <nivua1fn@duck.com>
_repo='Joshua-Ashton/dxvk-native'
_ver=native-1.9.2a
_auto_update=true
_auto_update_regexp="\d+\.\d+\.\d+"

pkgbase=libdxvk
pkgname=('libdxvk' 'lib32-libdxvk')
pkgver=1.9.2a
pkgrel=1
pkgdesc='DXVK Native is a port of DXVK to Linux which allows it to be used natively without Wine'
arch=('x86_64')
url="https://github.com/$_repo"
license=('zlib')

depends=('sdl2' 'vulkan-icd-loader' 'lib32-sdl2' 'lib32-vulkan-icd-loader' 'lib32-gcc-libs')
makedepends=('gcc' 'meson' 'glslang')

source=("dxvk-native-$_ver.tar.gz::$url/archive/native-$pkgver.tar.gz")
sha256sums=('SKIP')

build() {
  echo "==> Building 64-bit..."

  cd "$srcdir/dxvk-native-$_ver"
  meson --buildtype "release" \
    --prefix "/usr/" \
    --strip \
    -Denable_tests=true \
    "$srcdir/build-64"

  ninja -C "$srcdir/build-64"

  echo "==> Building 32-bit..."

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "$srcdir/dxvk-native-$_ver"
  meson --buildtype "release" \
    --prefix "/usr/" \
    --libdir lib32 \
    --strip \
    -Denable_tests=true \
    "$srcdir/build-32"

  ninja -C "$srcdir/build-32"
}

package_libdxvk() {
  depends=('sdl2' 'vulkan-icd-loader')

  DESTDIR="$pkgdir" ninja -C "$srcdir/build-64" install
  install -Dm 644 "$srcdir/dxvk-native-$_ver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENCE"
}

package_lib32-libdxvk() {
  pkgdesc="$pkgdesc (32-bit)"
  depends=('lib32-sdl2' 'lib32-vulkan-icd-loader' 'lib32-gcc-libs')

  DESTDIR="$pkgdir" ninja -C "$srcdir/build-32" install
  install -Dm 644 "$srcdir/dxvk-native-$_ver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENCE"
}
