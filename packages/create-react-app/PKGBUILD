# Maintainer: MedzikUser <nivua1fn@duck.com>
pkgname='create-react-app'
replaces=("nodejs-$pkgname")
provides=("nodejs-$pkgname")
pkgver='5.0.1'
pkgrel=1
pkgdesc='Create React apps with no build configuration'
arch=('any')
url="https://www.npmjs.com/package/$pkgname"
license=('MIT')

depends=('nodejs')
makedepends=('npm')

_file="$pkgname-$pkgver.tgz"
source=("https://registry.npmjs.org/$pkgname/-/$_file")
noextract=("$_file")
sha512sums=('524c5328af08f47b99647aa436afc990a5c381d2de89119cf9b2f9f0869eda0fbb5b01689808f8c29a67e897ea95933620a611959b602e5995af9a197dd775c6')

package() {
  npm install -g --prefix "$pkgdir/usr" "$srcdir/$_file"

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
