# Maintainer: MedzikUser <nivua1fn@duck.com>
pkgname='golangci-lint'
pkgver='1.46.1'
pkgrel=1
pkgdesc='Linters Runner for Go. 5x faster than gometalinter'
arch=('x86_64')
url="https://github.com/golangci/golangci-lint"
license=('GPL3')

makedepends=('go')

source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('c9f6a8cdef93b63b3d8a7b43c274a5961191de559037d28bf2152ca0e23883e5')

build() {
  cd "$pkgname-$pkgver"

  local _flags
  _flags=(
    -X main.version="$pkgver"
    -X main.date="$(date -u -d "@$SOURCE_DATE_EPOCH" +'%FT%TZ')"
  )

  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -modcacherw"

  go build \
    -v \
    -ldflags "${_flags[*]}" \
    -o bin/$pkgname \
    ./cmd/$pkgname
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 bin/$pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
