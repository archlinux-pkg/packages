# Maintainer: MedzikUser <nivua1fn@duck.com>
_repo='golangci/golangci-lint'
_ver=v1.44.0
_auto_update=true

_pkgname='golangci-lint'
pkgname="${_pkgname}"
pkgver=1.44.0
pkgrel=1
pkgdesc='Linters Runner for Go. 5x faster than gometalinter'
arch=('x86_64')
url="https://github.com/${_repo}"
license=('GPL3')

depends=('glibc')
makedepends=('git' 'go')

source=("${_pkgname}::git+${url}#tag=${_ver}")
sha256sums=('SKIP')

build() {
  cd "${_pkgname}"

  local _flags
  _flags=(
    -s -w
    -X main.version="${pkgver}"
    -X main.date="$(date -u -d "@${SOURCE_DATE_EPOCH}" +'%FT%TZ')"
  )

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -modcacherw"

  go build \
    -v \
    -ldflags "${_flags[*]}" \
    -o bin/${_pkgname} \
    ./cmd/${_pkgname}
}

package() {
  cd "${_pkgname}"

  install -Dm 755 bin/${_pkgname} "${pkgdir}/usr/bin/${_pkgname}"
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
