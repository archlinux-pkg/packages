# Maintainer: MedzikUser <nivua1fn@duck.com>
_npm='nodemon'
_ver=2.0.15
_auto_update=true
_auto_update_npm=true

pkgname="$_npm"
pkgver=2.0.15
pkgrel=1
replaces=("nodejs-$_npm")
provides=("nodejs-$_npm")
pkgdesc='Monitor for any changes in your node.js application and automatically restart the server - perfect for development'
arch=('any')
url="https://www.npmjs.com/package/$_npm"
license=('MIT')

depends=('nodejs')
makedepends=('npm')

_file="$_npm-$pkgver.tgz"
source=("https://registry.npmjs.org/$_npm/-/$_file")
noextract=("$_file")
sha256sums=('7d2a0113b58ae36fa418a4fe517d76e3fe96d2fa644ceea1bba90d9d2838acd8')

package() {
  npm install -g --prefix "$pkgdir/usr" "$srcdir/$_file"

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
