# Maintainer: MedzikUser <nivua1fn@duck.com>
pkgname='nodemon'
replaces=("nodejs-$pkgname")
provides=("nodejs-$pkgname")
pkgver='2.0.16'
pkgrel=1
pkgdesc='Monitor for any changes in your node.js application and automatically restart the server - perfect for development'
arch=('any')
url="https://www.npmjs.com/package/$pkgname"
license=('MIT')

depends=('nodejs')
makedepends=('npm')

_file="$pkgname-$pkgver.tgz"
source=("https://registry.npmjs.org/$pkgname/-/$_file")
noextract=("$_file")
sha256sums=('f2f02a4152dbf220e3b814f02b150e1fedf558ef622a14c1e918c74ebc24ca77')

package() {
  npm install -g --prefix "$pkgdir/usr" "$srcdir/$_file"

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
