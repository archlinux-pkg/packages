# Maintainer: MedzikUser <nivua1fn@duck.com>
pkgname='universal-android-debloater'
pkgver=0.4.1
pkgrel=1
pkgdesc='Cross-platform GUI written in Rust using ADB to debloat non-rooted android devices. Improve your privacy, the security and battery life of your device'
arch=('x86_64')
url="https://github.com/0x192/universal-android-debloater"
license=('GPL3')

depends=('android-tools' 'expat' 'freetype2')
makedepends=('cargo-nightly' 'libxkbcommon' 'pkg-config')

source=(
  "$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz"
  'uad_gui.desktop'
  'uad_gui-opengl.desktop'
)
sha256sums=(
  'SKIP'
  'SKIP'
  'SKIP'
)
options=('!lto')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  export RUSTUP_TOOLCHAIN=nightly
  cargo fetch --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  export RUSTUP_TOOLCHAIN=nightly

  # OpenGL
  echo '==> Compiling with OpenGL...'
  cargo build --release --features glow
  mv target/release/uad_gui target/release/uad_gui-opengl

  # Vulkan
  echo '==> Compiling with Vulkan...'
  cargo build --release --features wgpu
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -Dm 755 target/release/uad_gui{,-opengl} -t "$pkgdir/usr/bin"
  install -Dm 644 "$srcdir"/uad_gui{,-opengl}.desktop -t "$pkgdir/usr/share/applications"

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
