# Maintainer: MedzikUser <nivua1fn@duck.com>
_repo='google/log4jscanner'
_ver=v0.4.0
_auto_update=true

pkgname='log4jscanner'
pkgver=0.4.0
pkgrel=1
pkgdesc='log4j vulnerability filesystem scanner for analyzing JAR files'
arch=('x86_64')
url="https://github.com/$_repo"
license=('Apache')

makedepends=('go')

source=("$pkgname-$pkgver.tar.gz::$url/archive/$_ver.tar.gz")
sha256sums=('89ba6a9a20fd8342c21e224633f6103aa67b35a6f4e59e28861811300a7012b8')

build() {
  cd "$pkgname-$pkgver"

  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -modcacherw"

  go build \
    -v \
    -o bin/$pkgname \
    .
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 bin/$pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
