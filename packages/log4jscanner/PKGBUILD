# Maintainer: MedzikUser <nivua1fn@duck.com>
pkgname='log4jscanner'
pkgver='0.5.0'
pkgrel=1
pkgdesc='log4j vulnerability filesystem scanner for analyzing JAR files'
arch=('x86_64')
url="https://github.com/google/log4jscanner"
license=('Apache')

makedepends=('go')

source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('dea418c019c2bc251fec172bd632105e37cc6733ce8cec2ab87e27cae805696d')

build() {
  cd "$pkgname-$pkgver"

  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -modcacherw"

  go build \
    -v \
    -o bin/$pkgname \
    .
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 bin/$pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
