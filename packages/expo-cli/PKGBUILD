# Maintainer: MedzikUser <nivua1fn@duck.com>
pkgname='expo-cli'
replaces=("nodejs-$pkgname")
provides=("nodejs-$pkgname")
pkgver='5.4.2'
pkgrel=1
pkgdesc='Tools for creating, running, and deploying universal Expo and React Native apps'
arch=('any')
url="https://expo.io"
license=('MIT')

depends=('nodejs')
makedepends=('npm')

_file="$pkgname-$pkgver.tgz"
source=("$_file::https://registry.npmjs.org/$pkgname/-/$_file")
noextract=("$_file")
sha512sums=('5450fb8d459639cd33afd7f746a5db6f92739420dae0175ecbbbba757cec3f012a301fc2b99b04b2f03588634f097f05c9cac7736884b84f25ced4f0586e679a')

package() {
  npm install -g --prefix "$pkgdir/usr" "$srcdir/$_file"

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
