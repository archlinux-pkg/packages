# Maintainer: MedzikUser <nivua1fn@duck.com>
pkgname='expo-cli'
replaces=("nodejs-$pkgname")
provides=("nodejs-$pkgname")
pkgver='5.3.1'
pkgrel=1
pkgdesc='Tools for creating, running, and deploying universal Expo and React Native apps'
arch=('any')
url="https://expo.io"
license=('MIT')

depends=('nodejs')
makedepends=('npm')

_file="$pkgname-$pkgver.tgz"
source=("$_file::https://registry.npmjs.org/$pkgname/-/$_file")
noextract=("$_file")
sha512sums=('88d24b75ed870d18904cd18196135f59c4d040228ec19a17b25e79615d9895159689684b09616b0bcfa3e4ec43d8f46246ab9e48eb7047e29ff8e0c3eb474100')

package() {
  npm install -g --prefix "$pkgdir/usr" "$srcdir/$_file"

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
