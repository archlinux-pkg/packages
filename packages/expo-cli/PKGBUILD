# Maintainer: MedzikUser <nivua1fn@duck.com>
pkgname='expo-cli'
replaces=("nodejs-$pkgname")
provides=("nodejs-$pkgname")
pkgver='5.4.3'
pkgrel=1
pkgdesc='Tools for creating, running, and deploying universal Expo and React Native apps'
arch=('any')
url="https://expo.io"
license=('MIT')

depends=('nodejs')
makedepends=('npm')

_file="$pkgname-$pkgver.tgz"
source=("$_file::https://registry.npmjs.org/$pkgname/-/$_file")
noextract=("$_file")
sha512sums=('313db5115fbb7b9deaa977111996a8be0b8e5411b89a6a66e89a599c2e60ad002c5a7e897fe630124e5750e46123233615cad07e7b1e00926e440d4e638933af')

package() {
  npm install -g --prefix "$pkgdir/usr" "$srcdir/$_file"

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
