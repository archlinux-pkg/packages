# Maintainer: MedzikUser <nivua1fn@duck.com>
pkgname='expo-cli'
pkgver=5.3.0
pkgrel=1
pkgdesc='Tools for creating, running, and deploying universal Expo and React Native apps'
arch=('any')
url="https://expo.io"
license=('MIT')

depends=('nodejs')
makedepends=('npm')

_file="$pkgname-$pkgver.tgz"
source=("$_file::https://registry.npmjs.org/$pkgname/-/$_file")
noextract=("$_file")
sha512sums=('3a2e44f82dc0b401cec2a05b2f718bd5e404adf0e7646988a9b3239431a9f16b442cdd1ff9f75f580e1995d08e4a39eab1883b85840582b6578c85db8d8b02f5')

package() {
  npm install -g --prefix "$pkgdir/usr" "$srcdir/$_file"

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
