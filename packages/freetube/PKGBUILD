# Maintainer: MedzikUser

_repo='FreeTubeApp/FreeTube'
_ver=v0.16.0-beta
_auto_update=true

pkgname='freetube'
pkgdesc='An Open Source YouTube app for privacy'
pkgver=0.16.0_beta
pkgrel=1
arch=('x86_64')
url="https://github.com/${_repo}"
license=('AGPL3')
_electron='electron'
depends=("${_electron}" 'gtk3' 'nss')
makedepends=('git' 'nodejs' 'npm' 'yarn')
source=(
  "${pkgname}::git+${url}#tag=${_ver}"
  "package-only-necessary.diff"
  "freetube.desktop"
  "freetube.sh"
)
sha256sums=(
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
)

prepare() {
  patch "${pkgname}/_scripts/build.js" < package-only-necessary.diff
}

build() {
  cd "${pkgname}"

  yarn --cache-folder "${srcdir}/yarn-cache" install
  npm --cache "${srcdir}/npm-cache" run build
}

package() {
  install -d "${pkgdir}/usr/lib/${pkgname}"
  cp -R "./${pkgname}/build/linux-unpacked/resources/app.asar" "${pkgdir}/usr/lib/${pkgname}"

  install -Dm 755 "./freetube.sh" "${pkgdir}/usr/bin/${pkgname}"

  install -Dm 644 "${pkgname}/_icons/icon.svg" "${pkgdir}/usr/share/pixmaps/${pkgname}.svg"

  install -Dm 644 "freetube.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  install -Dm 644 "${pkgname}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
