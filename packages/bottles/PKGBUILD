# Maintainer: MedzikUser

_repo='bottlesdevs/Bottles'
_ver=2022.1.14-trento-1
_auto_update=true

_pkgname='bottles'
pkgname="${_pkgname}"
pkgver=2022.1.14_trento_1
pkgrel=1
pkgdesc='Easily manage wine prefixes in a new way. Run Windows software and games on Linux'
arch=('x86_64')
url="https://github.com/${_repo}"
license=('GPL3')
depends=(
  'hicolor-icon-theme'
  'dconf'
  'python'
  'libhandy'
  'gtk3'
  'patool'
  'p7zip'
  'python-gobject'
  'python-requests'
  'python-yaml'
  'python-markdown'
  'wine'
  'cabextract'
  'webkit2gtk'
  'xorg-xdpyinfo'
)
makedepends=('meson' 'ninja')
optdepends=(
  'gvfs'
  'vkd3d'
  'lib32-vkd3d'
  'lib32-vulkan-icd-loader'
  'vulkan-icd-loader'
  'gamemode'
)
source=("${_pkgname}::git+${url}#tag=${_ver}")
sha256sums=('SKIP')

build() {
  cd "${_pkgname}"

  meson --prefix='/usr' build
  ninja -C build
}

package() {
  cd "${_pkgname}"

  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
