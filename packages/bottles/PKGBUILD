# Maintainer: MedzikUser

_repo='bottlesdevs/Bottles'
_ver=2021.12.28-treviso

_pkgname='bottles'
pkgname="$_pkgname"
provides=("$_pkgname")
conflicts=("$_pkgname")
pkgver=2021.12.28_treviso
pkgrel=2
pkgdesc='Easily manage wine prefixes in a new way. Run Windows software and games on Linux'
arch=('x86_64')
url="https://github.com/$_repo"
license=('GPL3')
depends=(
  'hicolor-icon-theme'
  'dconf'
  'python'
  'libhandy'
  'gtk3'
  'patool'
  'p7zip'
  'python-gobject'
  'python-requests'
  'python-yaml'
  'python-markdown'
  'wine'
  'cabextract'
  'webkit2gtk'
  'xorg-xdpyinfo'
)
makedepends=('meson' 'ninja')
optdepends=(
  'gvfs'
  'vkd3d'
  'lib32-vkd3d'
  'lib32-vulkan-icd-loader'
  'vulkan-icd-loader'
  'gamemode'
)
source=("$_pkgname::git+$url#tag=$_ver")
sha256sums=('SKIP')

build() {
  cd "$srcdir/$_pkgname"

  meson --prefix='/usr' build
  ninja -C build
}

package() {
  cd "$srcdir/$_pkgname"

  DESTDIR="$pkgdir" ninja -C build install
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
