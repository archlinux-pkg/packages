# Maintainer: MedzikUser <nivua1fn@duck.com>
pkgname='create-next-app'
replaces=("nodejs-$pkgname")
provides=("nodejs-$pkgname")
pkgver='12.1.5'
pkgrel=1
pkgdesc='Create Next.js-powered React apps with one command'
arch=('any')
url="https://www.npmjs.com/package/$pkgname"
license=('MIT')

depends=('nodejs')
makedepends=('npm')

_file="$pkgname-$pkgver.tgz"
source=("https://registry.npmjs.org/$pkgname/-/$_file")
noextract=("$_file")
sha512sums=('9dd97825877ea00a728909516ed923052240c24e850600df25e789856bf0d3a673ae6bac078e4f8983e2153594eb3a5a5f3b136c950a7991f8147c669cab9a6b')

package() {
  npm install -g --prefix "$pkgdir/usr" "$srcdir/$_file"

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
