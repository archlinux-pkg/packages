# Maintainer: MedzikUser <nivua1fn@duck.com>
pkgname='create-next-app'
pkgver='12.1.1'
pkgrel=1
pkgdesc='Create Next.js-powered React apps with one command'
arch=('any')
url="https://www.npmjs.com/package/$pkgname"
license=('MIT')

depends=('nodejs')
makedepends=('npm')

_file="$pkgname-$pkgver.tgz"
source=("https://registry.npmjs.org/$pkgname/-/$_file")
noextract=("$_file")
sha512sums=('be8ab9237e31f5dce4deccdb89aac8e8be54426bf48114c1f21e1f555fceb6517108fd30ccb3bceff7517a9ecb5759ad473696f9d5e5fc3efbc7f56ce563f694')

package() {
  npm install -g --prefix "$pkgdir/usr" "$srcdir/$_file"

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
