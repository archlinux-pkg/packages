# Maintainer: MedzikUser <nivua1fn@duck.com>
pkgname='create-next-app'
replaces=("nodejs-$pkgname")
provides=("nodejs-$pkgname")
pkgver='12.1.4'
pkgrel=2
pkgdesc='Create Next.js-powered React apps with one command'
arch=('any')
url="https://www.npmjs.com/package/$pkgname"
license=('MIT')

depends=('nodejs')
makedepends=('npm')

_file="$pkgname-$pkgver.tgz"
source=("https://registry.npmjs.org/$pkgname/-/$_file")
noextract=("$_file")
sha512sums=('a04621cb51c5897c731baf0a3c9dcebc52ed0a2d1f02f40842667f05fa6d2ac6fbf1c884984e1800ab60f5bf809e4a7a4d70cb95a4b85653b3347427f3690785')

package() {
  npm install -g --prefix "$pkgdir/usr" "$srcdir/$_file"

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
