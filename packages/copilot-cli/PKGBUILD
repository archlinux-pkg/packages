# Maintainer: MedzikUser <nivua1fn@duck.com>
pkgname='copilot-cli'
pkgver='1.18.1'
pkgrel=1
pkgdesc='A tool to help deploy containerized applications on Amazon ECS'
arch=('x86_64')
url="https://github.com/aws/copilot-cli"
license=('Apache')

makedepends=('go')

source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz")
sha256sums=('a0a98013bfb8f73c0a2407aefa8f0b5539c85eec69bb1c111b6cb67b182d5f79')

build() {
  cd "$pkgname-$pkgver"

  local _flags
  _flags=(
    -X github.com/aws/copilot-cli/internal/pkg/version.Version=v$pkgver
  )

  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -modcacherw"

  go build \
    -v \
    -ldflags "${_flags[*]}" \
    -o bin/$pkgname \
    ./cmd/copilot
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 bin/$pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
