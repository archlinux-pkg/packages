# Maintainer: MedzikUser <nivua1fn@duck.com>
_pkgname='@angular/cli'
pkgname='angular-cli'
pkgver='13.3.1'
pkgrel=1
pkgdesc='CLI tool for Angular'
arch=('any')
url="https://www.npmjs.com/package/$_pkgname"
license=('MIT')

depends=('nodejs')
makedepends=('npm')

_file="cli-$pkgver.tgz"
source=("https://registry.npmjs.org/$_pkgname/-/$_file")
noextract=("$_file")
sha512sums=('d2ec14f2fdd5ff6b3de57e1c653e7cd89eaea51793fd9370fd501fe29e2ae7524df8106f74211bdb9d714c5f9370ea23c93a05c8962f83c4f6f174abb0db264d')

package() {
  npm install -g --prefix "$pkgdir/usr" "$srcdir/$_file"

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
