# Maintainer: MedzikUser <nivua1fn@duck.com>
_pkgname='@angular/cli'
pkgname='angular-cli'
replaces=("nodejs-$pkgname")
provides=("nodejs-$pkgname")
pkgver='13.3.6'
pkgrel=1
pkgdesc='CLI tool for Angular'
arch=('any')
url="https://www.npmjs.com/package/$_pkgname"
license=('MIT')

depends=('nodejs')
makedepends=('npm')

_file="cli-$pkgver.tgz"
source=("https://registry.npmjs.org/$_pkgname/-/$_file")
noextract=("$_file")
sha512sums=('f8e0bbbaca5af280c64337299a5dc323c5f22ef62eae148585c98b3ecc98fe7687015efc34a48d7f774858c3cda33022a03ce46573d95c7d1dc1275bf9c39e40')

package() {
  npm install -g --prefix "$pkgdir/usr" "$srcdir/$_file"

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
