# Maintainer: MedzikUser <nivua1fn@duck.com>
_pkgname='@angular/cli'
pkgname='angular-cli'
replaces=("nodejs-$pkgname")
provides=("nodejs-$pkgname")
pkgver='13.3.2'
pkgrel=1
pkgdesc='CLI tool for Angular'
arch=('any')
url="https://www.npmjs.com/package/$_pkgname"
license=('MIT')

depends=('nodejs')
makedepends=('npm')

_file="cli-$pkgver.tgz"
source=("https://registry.npmjs.org/$_pkgname/-/$_file")
noextract=("$_file")
sha512sums=('b1f84c6e6d300406261bef18118619e50905cafd9326632e6bf2db1a8244ff2e5428137eccbb0c3b71bf096dc3ea959385b81fd38a90a62d8b1e89db3c2fc192')

package() {
  npm install -g --prefix "$pkgdir/usr" "$srcdir/$_file"

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
