# Maintainer: MedzikUser <nivua1fn@duck.com>
_pkgname='@angular/cli'
pkgname='angular-cli'
replaces=("nodejs-$pkgname")
provides=("nodejs-$pkgname")
pkgver='13.3.5'
pkgrel=1
pkgdesc='CLI tool for Angular'
arch=('any')
url="https://www.npmjs.com/package/$_pkgname"
license=('MIT')

depends=('nodejs')
makedepends=('npm')

_file="cli-$pkgver.tgz"
source=("https://registry.npmjs.org/$_pkgname/-/$_file")
noextract=("$_file")
sha512sums=('16b3e0f3a71f9a6d1aad66489ede799ae093a5c412365be856256b215932a92374e86a32080436ce7ebf398267c7f5c083f5ef5d36f280bfb9f1cd165ee39a88')

package() {
  npm install -g --prefix "$pkgdir/usr" "$srcdir/$_file"

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
