# Maintainer: MedzikUser <nivua1fn@duck.com>
_npm='@angular/cli'
_ver=13.3.0
_auto_update=true
_auto_update_npm=true

pkgname='angular-cli'
pkgver=13.3.0
pkgrel=1
pkgdesc='CLI tool for Angular'
arch=('any')
url="https://www.npmjs.com/package/$_npm"
license=('MIT')

depends=('nodejs')
makedepends=('npm')

_file="cli-$pkgver.tgz"
source=("https://registry.npmjs.org/$_npm/-/$_file")
noextract=("$_file")
sha256sums=('88375412ac98d4dfbddfc0327dead44f1e361ebcf4bb21e9a1d5679a6c517154')

package() {
  npm install -g --prefix "$pkgdir/usr" "$srcdir/$_file"

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
