# Maintainer: MedzikUser <nivua1fn@duck.com>
_npm='@angular/cli'
_ver=13.2.6
_auto_update=true

pkgname='angular-cli'
pkgver=13.2.6
pkgrel=1
pkgdesc='CLI tool for Angular'
arch=('any')
url="https://www.npmjs.com/package/${_npm}"
license=('MIT')

depends=('nodejs')
makedepends=('npm')

_file="cli-$pkgver.tgz"
source=("https://registry.npmjs.org/${_npm}/-/${_file}")
noextract=("${_file}")
sha256sums=('84d2c5ccdf3e79bebca3b21a12d9f13c8dfd0e8eb30c7f4aa2e9b10c89a86c92')

package() {
  npm install -g --prefix "$pkgdir/usr" "${_file}"

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
