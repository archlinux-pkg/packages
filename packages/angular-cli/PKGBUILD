# Maintainer: MedzikUser <nivua1fn@duck.com>
_pkgname='@angular/cli'
pkgname='angular-cli'
replaces=("nodejs-$pkgname")
provides=("nodejs-$pkgname")
pkgver='13.3.7'
pkgrel=1
pkgdesc='CLI tool for Angular'
arch=('any')
url="https://www.npmjs.com/package/$_pkgname"
license=('MIT')

depends=('nodejs')
makedepends=('npm')

_file="cli-$pkgver.tgz"
source=("https://registry.npmjs.org/$_pkgname/-/$_file")
noextract=("$_file")
sha512sums=('5c8a74c3460ec211e9e097b79e91c0b345b8ae31ef1671b6c3f9433b633f50da79eb7e12e0f44c16655532de834018f571b7df615285a9f7ea7acc82a8dbaf01')

package() {
  npm install -g --prefix "$pkgdir/usr" "$srcdir/$_file"

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
