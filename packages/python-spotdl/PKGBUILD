# Maintainer: MedzikUser <nivua1fn@duck.com>
_repo='spotDL/spotify-downloader'
_ver=v3.9.3
_auto_update=true

_pkgname='spotdl'
pkgname="python-${_pkgname}"
pkgver=3.9.3
pkgrel=1
pkgdesc='Download your Spotify playlists and songs along with album art and metadata (from YouTube if a match is found)'
arch=('any')
url="https://github.com/${_repo}"
license=('MIT')

depends=(
  'ffmpeg'
  'yt-dlp'
  'python-spotipy'
  'python-pytube'
  'python-rich'
  'python-rapidfuzz'
  'python-mutagen'
  'python-ytmusicapi'
  'python-tqdm'
  'python-beautifulsoup4'
  'python-requests'
  'python-unidecode'
)
makedepends=('git' 'python-setuptools')

source=("${_pkgname}::git+${url}#tag=${_ver}")
sha256sums=('SKIP')

build() {
  cd "${_pkgname}"
  python setup.py build
}

package() {
  cd "${_pkgname}"

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
