# Maintainer: MedzikUser

_repo='spotDL/spotify-downloader'
_ver=v3.9.2

_pkgname='spotdl'
pkgname="python-$_pkgname"
provides=("$_pkgname")
conflicts=("$_pkgname")
pkgver=3.9.2
pkgrel=2
pkgdesc='Download your Spotify playlists and songs along with album art and metadata (from YouTube if a match is found)'
arch=('any')
url="https://github.com/$_repo"
license=('MIT')
depends=(
  'ffmpeg'
  'python-spotipy'
  'python-pytube'
  'python-rich'
  'python-rapidfuzz'
  'python-mutagen'
  'python-ytmusicapi'
  'yt-dlp'
  'python-tqdm'
  'python-beautifulsoup4'
  'python-requests'
  'python-unidecode'
)
makedepends=('jq' 'git' 'python-setuptools')
source=("$_pkgname::git+$url#tag=$_ver")
sha256sums=('SKIP')

build() {
  cd "$srcdir/$_pkgname"
  python setup.py build
}

package() {
  cd "$srcdir/$_pkgname"

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
