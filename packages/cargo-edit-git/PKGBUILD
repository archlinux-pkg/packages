# Maintainer: MedzikUser <nivua1fn@duck.com>
_repo='killercup/cargo-edit'
_ver=v0.8.0.r178.g56cc9079
_auto_update=true
_auto_update_git=true

pkgname='cargo-edit-git'
conflicts=('cargo-edit')
provides=('cargo-edit')
epoch=1
pkgver=v0.8.0.r178.g56cc9079
pkgrel=1
pkgdesc='Managing cargo dependencies from the command line (master branch from git)'
arch=('x86_64')
url="https://github.com/${_repo}"
license=('Apache' 'MIT')

depends=('cargo' 'libgit2.so')

source=("$pkgname::git+$url.git")
sha256sums=('SKIP')

build() {
  cd "$pkgname"

  CFLAGS+=' -ffat-lto-objects'
  cargo build --release
}

package() {
  cd "$pkgname"

  install -Dm 755 \
    'target/release/cargo-add' \
    'target/release/cargo-rm' \
    'target/release/cargo-upgrade' \
    -t "$pkgdir/usr/bin"

  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/${pkgname%-git}"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/${pkgname%-git}"
}
