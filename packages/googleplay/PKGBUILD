# Maintainer: MedzikUser <nivua1fn@duck.com>
_repo='89z/googleplay'
_ver=v1.7.0
_auto_update=true

pkgname='googleplay'
pkgver=1.7.0
pkgrel=2
pkgdesc='Download APK or send API requests'
arch=('x86_64')
url="https://github.com/$_repo"
license=('OSL3')

makedepends=('go')

source=("$pkgname-$pkgver.tar.gz::$url/archive/$_ver.tar.gz")
sha256sums=('2ba89721d0daee1da9ad2adea167e60e1e06bd476847ae8fbec9c7b9f65905b3')

build() {
  cd "$pkgname-$pkgver"

  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -modcacherw"

  go build \
    -v \
    -o bin/$pkgname \
    ./cmd/$pkgname
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 bin/$pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 license.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
