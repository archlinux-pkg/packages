# Maintainer: MedzikUser <nivua1fn@duck.com>
pkgname='googleplay'
pkgver='1.9.1'
pkgrel=1
pkgdesc='Download APK or send API requests'
arch=('x86_64')
url="https://github.com/89z/googleplay"
license=('OSL3')

makedepends=('go')

source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('12334d07bf6fb890aaca4bc8c332a1d38dd630795ba2fabb0a91adec7e6548c9')

build() {
  cd "$pkgname-$pkgver"

  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -modcacherw"

  go build \
    -v \
    -o bin/$pkgname \
    ./cmd/$pkgname
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 bin/$pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 license.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
