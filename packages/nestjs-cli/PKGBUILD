# Maintainer: MedzikUser <nivua1fn@duck.com>
_npm='@nestjs/cli'
_ver=8.1.8
_auto_update=true
_auto_update_npm=true

pkgname='nestjs-cli'
pkgver=8.1.8
pkgrel=1
replaces=("nodejs-$pkgname")
provides=("nodejs-$pkgname")
pkgdesc='CLI tool for Nest applications'
arch=('any')
url="https://docs.nestjs.com/cli/overview"
license=('MIT')

depends=('nodejs')
makedepends=('npm')

_file="cli-$pkgver.tgz"
source=("https://registry.npmjs.org/$_npm/-/$_file")
noextract=("$_file")
sha256sums=('d1e3e6fa67129af368b95fdefc9e0d9a48b3cc2cb03d993cc932c0bf6171010e')

package() {
  npm install -g --prefix "$pkgdir/usr" "$_file"

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
