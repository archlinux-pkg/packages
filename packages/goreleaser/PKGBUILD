# Maintainer: MedzikUser

_repo='goreleaser/goreleaser'
_ver=v1.2.5
_auto_update=true

_pkgname='goreleaser'
pkgname="$_pkgname"
provides=("$_pkgname")
conflicts=("$_pkgname")
pkgver=1.2.5
pkgrel=1
pkgdesc='Deliver Go binaries as fast and easily as possible'
arch=('x86_64')
url="https://github.com/$_repo"
license=('MIT')
depends=('go')
makedepends=('git' 'go')
source=("$_pkgname::git+$url#tag=$_ver")
sha256sums=('SKIP')

build() {
  cd "$_pkgname"

  local _flags
  _flags=(
    -s -w
    -X main.version="$pkgver"
    -X main.builtBy="medzikuser"
  )

  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -modcacherw"

  go build \
    -v \
    -ldflags "${_flags[*]}" \
    -o bin/$_pkgname \
    .
}

package() {
  cd "$_pkgname"

  install -Dm 755 bin/$_pkgname "$pkgdir/usr/bin/$_pkgname"
  install -Dm 644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
