# Maintainer: MedzikUser <nivua1fn@duck.com>
pkgname="goreleaser"
pkgver='1.7.0'
pkgrel=2
pkgdesc='Deliver Go binaries as fast and easily as possible'
arch=('x86_64')
url="https://github.com/goreleaser/goreleaser"
license=('MIT')

depends=('go')
makedepends=('go')

source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('12d313e2eb91aef656f7e8929b450665fec8473959e9b40efaceaaf5182dd0b6')

build() {
  cd "$pkgname-$pkgver"

  local _flags
  _flags=(
    -X main.version="$pkgver"
    -X main.builtBy="medzikuser"
  )

  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -modcacherw"

  go build \
    -v \
    -ldflags "${_flags[*]}" \
    -o bin/$pkgname \
    .
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 bin/$pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
