# Maintainer: MedzikUser

_repo='stuarthayhurst/alphabetical-grid-extension'
_ver=v18.0
_auto_update=true

_pkgname='alphabetical-grid-extension'
pkgname="gnome-shell-extension-${_pkgname}"
pkgver=18.0
pkgrel=1
pkgdesc='Restore the alphabetical ordering of the app grid, removed in GNOME 3.38'
arch=('any')
url="https://github.com/${_repo}"
license=('GPL3')
depends=('gnome-shell>=3.38')
source=("${pkgname}::git+${url}.git#tag=${_ver}")
sha256sums=('SKIP')

build() {
  cd "${pkgname}"
  make build
}

package() {
  local _uuid='AlphabeticalAppGrid@stuarthayhurst'
  local _install_dir="${pkgdir}/usr/share/gnome-shell/extensions/${_uuid}"

  install -d ${_install_dir}

  cd "${pkgname}"

  unzip ${_uuid}.shell-extension.zip -d ${_install_dir}
  install -Dm644 "schemas/org.gnome.shell.extensions.AlphabeticalAppGrid.gschema.xml" -t "${pkgdir}/usr/share/glib-2.0/schemas/"

  install -Dm644 "LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
