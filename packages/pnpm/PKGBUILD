# Maintainer: MedzikUser <nivua1fn@duck.com>
pkgname='pnpm'
pkgver='7.1.1'
pkgrel=1
pkgdesc='Fast, disk space efficient package manager'
arch=('any')
url="https://www.npmjs.com/package/$pkgname"
license=('MIT')

depends=('nodejs')
makedepends=('npm')

_file="$pkgname-$pkgver.tgz"
source=("https://registry.npmjs.org/$pkgname/-/$_file")
noextract=("$_file")
sha512sums=('45d5962bfa83dd051ff0bb412793200f7087a45707b8ef72373fbfd1147c18376ed2d2354e705298e4f1914196c5aec06a459929c3ad0c098b822fbe68a6cb9e')

package() {
  npm install -g --prefix "$pkgdir/usr" "$_file"

  # Delete .map files
  find "$pkgdir"/usr/lib -depth -name '*.map' -delete

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
