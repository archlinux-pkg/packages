# Maintainer: MedzikUser <nivua1fn@duck.com>
pkgname='pnpm'
pkgver='6.32.9'
pkgrel=1
pkgdesc='Fast, disk space efficient package manager'
arch=('any')
url="https://www.npmjs.com/package/$pkgname"
license=('MIT')

depends=('nodejs')
makedepends=('npm')

_file="$pkgname-$pkgver.tgz"
source=("https://registry.npmjs.org/$pkgname/-/$_file")
noextract=("$_file")
sha512sums=('7d4feeac95efd90f25e27f055b8431e782860d354ca5ade7edacea6bb8bae5766d1b931ca5adf90fe1d39802ffec6deeff747d5863c6e0274b53beb7d538ba68')

package() {
  npm install -g --prefix "$pkgdir/usr" "$_file"

  # Delete .map files
  find "$pkgdir"/usr/lib -depth -name '*.map' -delete

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
