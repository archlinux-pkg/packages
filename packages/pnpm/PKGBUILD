# Maintainer: MedzikUser <nivua1fn@duck.com>
pkgname='pnpm'
pkgver='6.32.10'
pkgrel=1
pkgdesc='Fast, disk space efficient package manager'
arch=('any')
url="https://www.npmjs.com/package/$pkgname"
license=('MIT')

depends=('nodejs')
makedepends=('npm')

_file="$pkgname-$pkgver.tgz"
source=("https://registry.npmjs.org/$pkgname/-/$_file")
noextract=("$_file")
sha512sums=('d7e4ec724940296bc8ea2358d5c55f46c1d8f61e5d81aac7177cd87e0fb2572692dbbd2ebca950297067697e93e9819734658d5ea5a10d44bd108cca083b3b0f')

package() {
  npm install -g --prefix "$pkgdir/usr" "$_file"

  # Delete .map files
  find "$pkgdir"/usr/lib -depth -name '*.map' -delete

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
