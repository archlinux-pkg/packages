# Maintainer: MedzikUser <nivua1fn@duck.com>
_pkgname='@capacitor/cli'
pkgname='capacitor-cli'
replaces=("nodejs-$pkgname")
provides=("nodejs-$pkgname")
pkgver='3.5.0'
pkgrel=1
pkgdesc='Build cross-platform Native Progressive Web Apps for iOS, Android, and the Web'
arch=('any')
url="https://www.npmjs.com/package/$_pkgname"
license=('MIT')

depends=('nodejs')
makedepends=('npm')

_file="cli-$pkgver.tgz"
source=("$_file::https://registry.npmjs.org/$_pkgname/-/$_file")
noextract=("$_file")
sha256sums=('a9ff5c75b40d685849421dba175fa0f9ad7327819a1410eea3ae605bd4202e6e')

package() {
  npm install -g --prefix "$pkgdir/usr" "$srcdir/$_file"

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
