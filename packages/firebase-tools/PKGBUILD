# Maintainer: MedzikUser <nivua1fn@duck.com>
pkgname='firebase-tools'
pkgver='10.8.0'
pkgrel=1
pkgdesc='The Firebase Command Line Tools'
arch=('any')
url="https://www.npmjs.com/package/$pkgname"
license=('MIT')

depends=('nodejs')
makedepends=('npm')

_file="$pkgname-$pkgver.tgz"
source=("https://registry.npmjs.org/$pkgname/-/$_file")
noextract=("$_file")
sha512sums=('038ca9ef755887d88a1011f9f6605b0bd06041d22262caf730fdbc835b03d9b986b54e74354f68ed881d9e67f9ce65af1a1a481ad4ee7b5c0b8d83b0be2ffdfa')

package() {
  npm install -g --prefix "$pkgdir/usr" "$_file"

  # Delete .map files
  find "$pkgdir"/usr/lib -depth -name '*.map' -delete

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
