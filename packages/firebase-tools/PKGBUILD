# Maintainer: MedzikUser <nivua1fn@duck.com>
pkgname='firebase-tools'
pkgver='11.0.1'
pkgrel=1
pkgdesc='The Firebase Command Line Tools'
arch=('any')
url="https://www.npmjs.com/package/$pkgname"
license=('MIT')

depends=('nodejs')
makedepends=('npm')

_file="$pkgname-$pkgver.tgz"
source=("https://registry.npmjs.org/$pkgname/-/$_file")
noextract=("$_file")
sha512sums=('6dee57c7615b756d06d98329c6c21c35a16e7a47a24601513d27156d78677c72c839427f11360236b4049ddce1a6b5656b097a97a116f47d7c0d1c7ad48e5129')

package() {
  npm install -g --prefix "$pkgdir/usr" "$_file"

  # Delete .map files
  find "$pkgdir"/usr/lib -depth -name '*.map' -delete

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
